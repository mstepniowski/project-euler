;; Solution to Project Euler problem 44
;; http://projecteuler.net/index.php?section=problems&id=44
;;
;; Pentagonal numbers are generated by the formula, P_n = n * (3 * n - 1) / 2.
;; The first ten pentagonal numbers are:
;;
;; 1, 5, 12, 22, 35, 51, 70, 92, 117, 145, ...
;;
;; It can be seen that P_4 + P_7 = 22 + 70 = 92 = P_8. However, their
;; difference, 70 - 22 = 48, is not pentagonal.
;;
;; Find the pair of pentagonal numbers, P_j and P_k, for which their
;; sum and difference is pentagonal and D = |P_k - P_j| is minimised;
;; what is the value of D?
(ns com.stepniowski.euler-044)

(defn pentagonal-numbers []
  (map #(* % (dec (* 3 %)) 1/2) (iterate inc 1)))

(defn pentagonal? [n]
  (zero? (rem (inc (Math/sqrt (inc (* 24 n)))) 6)))

(defn solution []
  (let [pentagonals (pentagonal-numbers)]
    (first (for [p pentagonals
		 q (take-while #(< % p) pentagonals)
		 :when (and (pentagonal? (- p q)) (pentagonal? (+ p q)))]
	     (- p q)))))